plugins {
    id 'net.researchgate.release' version '2.3.3'
}

group 'com.shekhargulati.reactivex'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'org.twitter4j:twitter4j-stream:4.0.4'
    compile 'org.slf4j:slf4j-api:1.7.18'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.slf4j:slf4j-simple:1.7.18'
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv('OSS_USER'), password: System.getenv('OSS_PASSWORD'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv('OSS_USER'), password: System.getenv('OSS_PASSWORD'))
            }

            pom.project {
                name 'Rx-Tweet-Stream'
                packaging 'jar'
                artifactId 'rx-tweet-stream'
                description 'A RxJava Observable wrapper around Twitter Streaming API'
                url 'https://github.com/shekhargulati/rx-tweet-stream'

                scm {
                    connection 'https://github.com/shekhargulati/rx-tweet-stream.git'
                    developerConnection 'https://github.com/shekhargulati/rx-tweet-stream.git'
                    url 'https://github.com/shekhargulati/rx-tweet-stream.git'
                }

                licenses {
                    license {
                        name 'MIT'
                    }
                }

                developers {
                    developer {
                        id 'shekhargulati'
                        name 'Shekhar Gulati'
                        email 'shekhargulati84@gmail.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}